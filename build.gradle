
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


group = 'com.dev-smart'
version = '0.1.1-SNAPSHOT'
description = 'UBJSON lib for Java'

repositories {
  jcenter()
}

dependencies {
  testCompile 'junit:junit:4.11'

}

compileJava {
  sourceCompatibility = 1.6
  targetCompatibility = 1.6
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives sourceJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories.mavenDeployer {

    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: sonatypeUsername, password: sonatypePassword)
    }

    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
      authentication(userName: sonatypeUsername, password: sonatypePassword)
    }

    pom.project {
      name 'ubjson'
      description 'UBJSON lib for Java'
      url 'http://github.com/dinocore1/ubjson'

      scm {
        url 'scm:git@github.com:dinocore1/ubjson.git'
        connection 'scm:git@github.com:dinocore1/ubjson.git'
        developerConnection 'scm:git@github.com:dinocore1/ubjson.git'
      }

      licenses {
        license {
          name 'MIT License'
          url 'https://github.com/dinocore1/ubjson/blob/master/LICENSE.txt'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'psoucy'
          name 'Paul Soucy'
        }
      }
    }

  }
}